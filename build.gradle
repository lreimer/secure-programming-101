buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
		classpath 'com.thoughtworks.tools:dependency-check:0.0.8'
		classpath 'org.standardout:gradle-versioneye-plugin:1.4.0'
	}
}

apply plugin: 'org.standardout.versioneye'

versioneye {
	dependencies = transitive
	includeSubProjects = true
	includePlugins = false
	exclude 'testCompile', 'testRuntime'
}

allprojects {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'license'
	apply plugin: 'dependency-check'

	license {
		skipExistingHeaders = true
		header rootProject.file('LICENSE')
		includes(["**/*.java", "**/*.groovy"])
		mapping {
			java = 'SLASHSTAR_STYLE'
			groovy = 'SLASHSTAR_STYLE'
		}
	}

	group = 'de.qaware.campus.secpro'
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	dependencies {
		testCompile('org.testng:testng:6.8.21') {
			exclude group: 'junit'
		}
		testCompile 'org.hamcrest:hamcrest-library:1.3'
	}
	
	test {
		useTestNG()
	}
}

